#
# This file is part of Wolfo.
#
# Wolfo is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, either version 3 of the License, or
# (at your option) any later version.
#
# Wolfo is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with Wolfo.  If not, see <https://www.gnu.org/licenses/>.
#
# Copyright 2020 - Romitou <romitou@protonmail.com> and contributors
#

on script load:
	send join "&3&lWolfo &7&l▪ &8Took %difference between now and {wolfo::loading}%." to ops
	send "" to ops
	send "&3&lWolfo &7&l▪ &7Loading main ..." to ops
	set {wolfo::loading} to now
	wolfoInitialize()

function wolfoInitialize():
	set {wolfo::game::status} to "WAIT" if {wolfo::game::status} is not set
	set {wolfo::game::day} to "0" if {wolfo::game::day} is not set

function wolfoHelp(msg: text, p: player):
	send join wolfoGetText("PREFIX.GENERAL") and wolfoGetText("MESSAGE.%{_msg}%.TITLE", {_p}) to {_p}
	set {_help::%{_msg}%::*} to wolfoGetObjects("MESSAGE.%{_msg}%.MSG", {_p})
	loop {_help::%{_msg}%::*}:
		send colored loop-value to {_p}

function wolfoJoin(p: player):
	set {wolfo::players::lang::%{_p}%} to wolfoGetText("LANGUAGES.DEFAULT") if {wolfo::players::lang::%{_p}%} is not set
	loop players:
		send join wolfoGetText("PREFIX.GENERAL"), "&b%{_p}% ", wolfoGetText("MESSAGE.JOINED", loop-player) and " &8(&7%amount of players%&8/&75&8)" to loop-player
	set {_p}'s xp to 0
	teleport {_p} to wolfoGetLoc("GAME.LOCATIONS.BEGIN")
	set {_p}'s gamemode to adventure
	clear {_p}'s inventory
	heal {_p}
	reset {_p}'s walk speed
	set slot 0 of {_p} to enchanted book named wolfoGetText("ITEM.COSM", {_p})
	set slot 4 of {_p} to book named wolfoGetText("ITEM.INFO", {_p})
	set slot 8 of {_p} to orange bed named wolfoGetText("ITEM.EXIT", {_p})
	set slot 6 of {_p} to magma cream named wolfoGetText("ITEM.SETUP", {_p}) if {_p} is op
	set slot 2 of {_p} to ender pearl named wolfoGetText("ITEM.MANAGE", {_p}) if {_p} is op
	set {wolfo::players::role::%{_p}%} to "&7Aucun"
	set {wolfo::players::life::%{_p}%} to "ALIVE"
	wolfoSbPlayer({_p}, 0)
	wolfoDebug("%{_p}% joined the game.")
	wait 1 second
	send join wolfoGetText("PREFIX.GENERAL") and wolfoGetText("MESSAGE.WELCOME", {_p}) to {_p}

function wolfoLeave(p: player):
	loop players:
		send join wolfoGetText("PREFIX.GENERAL"), "&b%{_p}% ", wolfoGetText("MESSAGE.LEFT", loop-player) and " &8(&7%amount of players - 1%&8/&75&8)" to loop-player
	wolfoSbPlayer({_p}, 1)
	remove {_p} from {wolfo::role::list::%{wolfo::players::role_name::%{_p}%}%::*}
	remove {_p} from {wolfo::players::list::*}
	remove {_p} from {wolfo::players::alive::*}
	wolfoDebug("%{_p}% left the game.")

command /wolfo [<text>] [<text>] [<integer>]:
	trigger:
		if arg 1 is "help":
			wolfoHelp("COMMANDS", player)
		else if arg 1 is "setup":
			wolfoMenu(player, "SETUP")
		else if arg 1 is "manage":
			wolfoMenu(player, "MANAGE")
		else if arg 1 is "whitelist":
			wolfoWhitelist(player, arg-2, arg-3)
		else if arg 1 is "lang":
			wolfoLang(player, arg-2)
		else:
			wolfoHelp("COMMANDS", player)

function wolfoWhitelist(executor: player, target: text, action: integer):
	if {_executor} is not op:
		send join wolfoGetText("PREFIX.GENERAL") and wolfoGetText("MESSAGE.PERMISSION.MESSAGE", {_executor}) to {_executor}
	else:
		set {_check::*} to wolfoGetObjects("GAME.WHITELIST")
		if {_action} is 1:
			if {_check::*} do not contain {_target}:
				wolfoUpdateO("GAME.WHITELIST", {_target}, 0)
				send join wolfoGetText("PREFIX.GENERAL") and wolfoGetText("MESSAGE.WHITELISTED_SUCCESS", {_executor}) to {_executor}
			else:
				send join wolfoGetText("PREFIX.GENERAL") and wolfoGetText("MESSAGE.WHITELISTED_ALREADY", {_executor}) to {_executor}
		else if {_action} is 2:
			if {_check::*} contains {_target}:
				wolfoUpdateO("GAME.WHITELIST", {_target}, 1)
				send join wolfoGetText("PREFIX.GENERAL") and wolfoGetText("MESSAGE.NOT_WHITELISTED_SUCCESS", {_executor}) to {_executor}
			else:
				send join wolfoGetText("PREFIX.GENERAL") and wolfoGetText("MESSAGE.NOT_WHITELISTED_ALREADY", {_executor}) to {_executor}
		else if {_action} is not set:
			if {_target} is set:
				if {_check::*} do not contain {_target}:
					set {_add} to wolfoGetText("MESSAGE.ADD_WHITELIST", {_executor})
					set {_allow} to wolfoGetText("MESSAGE.ALLOW_ACCESS", {_executor})
					send formatted join wolfoGetText("PREFIX.GENERAL"), wolfoGetText("MESSAGE.NOT_WHITELISTED", {_executor}) and "          <cmd:/wolfo whitelist %{_target}% 1><tooltip:&a%{_add}%>&a[%{_allow}% %{_target}%]&r" to {_executor}
				else:

					set {_remove} to wolfoGetText("MESSAGE.REMOVE_WHITELIST", {_executor})
					set {_deny} to wolfoGetText("MESSAGE.DENY_ACCESS", {_executor})
					send formatted join wolfoGetText("PREFIX.GENERAL"), wolfoGetText("MESSAGE.WHITELISTED", {_executor}) and "          <cmd:/wolfo whitelist %{_target}% 2><tooltip:&c%{_remove}%>&c[%{_deny}% %{_target}%]&r" to {_executor}
			else:
				send join wolfoGetText("PREFIX.GENERAL") and wolfoGetText("MESSAGE.WHITELIST.LIST.1", {_executor}) to {_executor}
				if {_check::*} is not set:
					add wolfoGetText("MESSAGE.NO_PLAYER", {_executor}) to {_check::*}
				loop {_check::*}:
					send "&b[&6%loop-index%&b] &f%loop-value%" to {_executor}
				send wolfoGetText("MESSAGE.WHITELIST.LIST.2", {_executor}) to {_executor}

function wolfoCheckStart(p: player):
	if wolfoGetText("GAME.SETUP.FINISH") is "finished": # C'est moche, mais ça sera corrigé dans les prochaines versions. :)
		if {wolfo::debug::status} is true:
			wolfoTimer(5)
			stop
		if size of players < 5:
			send join wolfoGetText("PREFIX.GENERAL") and wolfoGetText("MESSAGE.NOTENOUGH", {_p}) to {_p}
		else:
			set {_pos} to size of node list "GAME.LOCATIONS.SPAWNPOINTS" from "config"
			if {_pos} is greater than or equal to size of players:
				send join wolfoGetText("PREFIX.GENERAL") and wolfoGetText("MESSAGE.FORCESTART", {_p}) to {_p}
				wolfoTimer(5)
				send join wolfoGetText("PREFIX.GENERAL") and wolfoGetText("MESSAGE.START.PREPARE", {_p}) to players
			else:
				set {_notenough} to wolfoGetText("MESSAGE.TP_NOT_ENOUGH", {_p})
				send join wolfoGetText("PREFIX.GENERAL") and "&c%{_notenough}% (%{_pos}%/%size of players%)" to {_p}
	else:
		send join wolfoGetText("PREFIX.GENERAL") and wolfoGetText("MESSAGE.MISSINGCONFIG", {_p}) to {_p}

function wolfoStart():
	{wolfo::debug::status} is true:
		set {wolfo::players::list::*} to {wolfo::debug::list::*}
		set {wolfo::players::alive::*} to {wolfo::debug::list::*}
	else:
		set {wolfo::players::list::*} to players
		set {wolfo::players::alive::*} to players
	loop {wolfo::players::list::*}:
		send join wolfoGetText("PREFIX.GENERAL") and wolfoGetText("MESSAGE.DEVELOPER_MODE", loop-value) to {wolfo::players::list::*} if {wolfo::debug::status} is true
		send join wolfoGetText("PREFIX.GENERAL") and wolfoGetText("MESSAGE.PROJECT.TEXT", loop-value) to {wolfo::players::list::*}
		set {_project::*} to wolfoGetObjects("MESSAGE.PROJECT.LIST", loop-value)
		send join {_project::*} by ", " to {wolfo::players::list::*}
		teleport loop-value to wolfoGetLoc("GAME.LOCATIONS.SPAWNPOINTS.%loop-index%")
		wolfoDebug("Teleport %loop-value%.")
	set time of world "world" to 12:00
	loop {wolfo::players::list::*}:
		clear {wolfo::cosmetic::%loop-value%::active}
		stopEffect "wolfo-%loop-value%"
	wait 5 seconds
	wolfoGiveRoles()

function wolfoEnd():
	loop players:
		kick loop-player due to join wolfoGetText("PREFIX.GENERAL") and wolfoGetText("MESSAGE.ENDGAME", loop-player)
	clear {wolfo::*}
	wolfoInitialize()

on join:
	set join message to ""
	if player is op:
		wolfoJoin(player)
	else if size of players is greater than or equal to 25:
		kick player due to join wolfoGetText("PREFIX.GENERAL") and wolfoGetText("MESSAGE.TOOMANY", player)
	else if {wolfo::game::start} is set:
		kick player due to join wolfoGetText("PREFIX.GENERAL") and wolfoGetText("MESSAGE.ALREADYSTART", player)
	else if wolfoGetText("GAME.PRIVACY") is "CLOSED":
		kick player due to join wolfoGetText("PREFIX.GENERAL") and wolfoGetText("MESSAGE.PRIVACY.CLOSED", player)
	else if wolfoGetText("GAME.PRIVACY") is "WHITELISTED":
		if wolfoGetObjects("GAME.WHITELIST") do not contain player:
			kick player due to join wolfoGetText("PREFIX.GENERAL") and wolfoGetText("MESSAGE.PRIVACY.WHITELISTED", player)
	else:
		wolfoJoin(player)

on quit:
	set quit message to ""
	wolfoLeave(player)

on click:
	if name of player's tool is wolfoGetText("ITEM.COSM", player):
		cancel event
		wolfoCosmetic(1, player)
	else if name of player's tool is wolfoGetText("ITEM.COSM_OFF", player):
		cancel event
		wolfoCosmetic(4, player)
	else if name of player's tool is wolfoGetText("ITEM.INFO", player):
		cancel event
		wolfoMenuRoles(player)
	else if name of player's tool is wolfoGetText("ITEM.VOTE", player):
		wolfoVote(player)
	else if name of player's tool is wolfoGetText("ITEM.EXIT", player):
		cancel event
		kick player due to join wolfoGetText("PREFIX.GENERAL") and wolfoGetText("MESSAGE.GOODLEFT", player)
	else if name of player's tool is wolfoGetText("ITEM.SETUP", player):
		cancel event
		wolfoMenu(player, "SETUP")
	else if name of player's tool is wolfoGetText("ITEM.MANAGE", player):
		cancel event
		wolfoMenu(player, "MANAGE")

on drop:
	cancel event

on hunger meter change:
	cancel event

on weather change:
	cancel event

on damage of player:
	cancel event
